model{
  # historical controls
  y[1] ~ dbin(theta1,n) 
  logit(theta1) <-  logit_theta1
  logit_theta1   ~  dnorm(mu, inv_tau2)  
               
  inv_tau2 <- pow(tau, -2)
  
  #Spike and Slab on logit_theta1
  t ~ dbern(p0);
  slab ~ dunif(l.slab,u.slab);
  tau <- (1-t)*spike + t*slab;
  mu  ~ dnorm(0,1.0E-6)
  

  # analysis of current data as joint model
  y[2] ~ dbin(theta2, n)
  #theta2 ~ dbeta(0.5, 0.5)
  logit(theta2) <-  logit_theta2
  logit_theta2 ~  dnorm(mu, inv_tau2) # commensurate prior
  
  y[3] ~ dbin(theta3, m)
  theta3 <- theta2+ diff
  diff ~ dnorm(0.0,1.0E-4)
 
  #post prob
  p <- step(diff)
  p_1 <- 1 - step(diff)

  #reference model
  y[4] ~ dbin(theta4,n) 
  theta4 ~ dbeta(0.5,0.5)

  # pooled model
  y[5] ~ dbin(theta5,n) 
  theta5 ~ dbeta(a, b)

  # predictions
  y2_pred ~ dbin(theta2, N)
  y3_pred ~ dbin(theta3, M)
  
  # PPD
  p_p <- step(y3_pred - y2_pred)
}

